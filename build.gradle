group 'com.google.androidstudio.poet'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.squareup:javapoet:1.10.0'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    compile 'junit:junit:4.12'

    compile group: 'commons-io', name: 'commons-io', version: '2.0.1'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-inline:2.10.0"
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

task runGenerator(type: JavaExec) {
    main = 'com.google.androidstudiopoet.AndroidStudioPoet'
    classpath = sourceSets.main.runtimeClasspath
}

// java -jar build/libs/java-generator-all-1.0-SNAPSHOT.jar
task fatJar(type: Jar) {
    manifest {
        attributes (
                'Main-Class': 'com.google.androidstudiopoet.AndroidStudioPoet',
        )
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
kotlin {
    experimental {
        coroutines "enable"
    }
}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/assets', 'src/main/assets/gradle.wrapper']
        }
    }
}
